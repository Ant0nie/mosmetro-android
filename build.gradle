buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath('fr.avianey.androidsvgdrawable:gradle-plugin:3.0.0') {
            exclude group: 'xerces'
        }
    }
}

apply plugin: 'com.android.application'
apply plugin: 'androidsvgdrawable'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId 'pw.thedrhax.mosmetro'
        minSdkVersion 11
        targetSdkVersion 25
        versionCode 53
        versionName "1.7.3"

        // GitHub Pages
        buildConfigField "String", "\$GITHUB", "\"https://thedrhax.github.io/mosmetro-android\""
        buildConfigField "String", "NEWS_URL", "\$GITHUB + \"/news.json\""
        buildConfigField "String", "API_URL_SOURCE", "\$GITHUB + \"/base-url\""

        // Default stat URL
        buildConfigField "String", "API_URL_DEFAULT", "\"https://mosmetro.duckdns.org\""

        // Relative URLs for stat server
        buildConfigField "String", "API_REL_STATISTICS", "\"/api/v1/statistics.php\""
        buildConfigField "String", "API_REL_BRANCHES", "\"/api/v1/branches.php\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile new File(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
    }

    sourceSets {
        // Test sources.
        test.java.srcDir 'src/test/java'
        androidTest.java.srcDir 'src/androidTest/java'

        main {
            manifest.srcFile 'AndroidManifest.xml'
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res', 'build/generated/res/main']
            assets.srcDirs = ['assets']
            java {
                srcDir 'src'
                exclude 'androidTest/**'
                exclude 'test/**'
            }
        }

        lintOptions {
            abortOnError false
        }
    }
}

import fr.avianey.androidsvgdrawable.gradle.SvgDrawableTask
task svgToDrawablePng(type: SvgDrawableTask) {
    from = files('res-svg/drawable')
    to = file('build/generated/res/main')
    createMissingDirectories = true
    overwriteMode = 'ifModified'
    targetedDensities = ['ldpi', 'hdpi', 'mdpi', 'xhdpi', 'xxhdpi', 'xxxhdpi']
    outputFormat = 'PNG'
    outputType = 'drawable'
}

repositories {
    jcenter()
}

dependencies {
    // Runtime dependencies.
    compile 'com.android.support:support-annotations:25.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'org.jsoup:jsoup:1.8.3'
    compile ('com.googlecode.json-simple:json-simple:1.1.1') {
        exclude module: 'junit'
    }

    // Test dependencies.
    testCompile 'junit:junit:4.12'

    // Needed because there is a conflict in 'com.android.support.test:runner' package.
    // Here we explicitly define the version for the 'androidTest' sources build variant.
    androidTestCompile 'com.android.support:support-annotations:25.1.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
}
